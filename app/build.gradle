apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.yanzhenjie.andserver'

def static buildTime() {
    return new Date().format("yyyyMMdd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
}

android {
    compileSdk 34

    defaultConfig {
        applicationId 'com.github.tvbox.osc.tk'
        minSdkVersion 19  // 兼容 Android 4.4
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.".concat(buildTime())
        multiDexEnabled true  // 启用 MultiDex
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
        }
    }

    flavorDimensions +=[ "abi","brand" ]
    productFlavors {
        armeabi {
            dimension = "abi"
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        arm64 {
            dimension = "abi"
            ndk {
                abiFilters 'arm64-v8a'
            }
        }

        generic  {
            dimension = "brand"
        }

        hisense {
            applicationId 'com.github.hisense.osc.tk'
            dimension = "brand"
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def flavorNames = variant.productFlavors.collect { it.name }.join('-')
            def fileName = "TVBox_${variant.buildType.name}-${flavorNames}.apk"
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildFeatures {
        dataBinding = true
    }

    lint {
        disable 'UnsafeOptInUsageError'
    }
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    api fileTree(dir: "libs", include: ['*.jar', '*.aar'])
    implementation 'org.conscrypt:conscrypt-android:2.2.1'  // 降级支持 API 19
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'  // 降级支持 4.4
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.6.0'  // 降级支持 4.4
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'  // 降级支持 4.4
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.commons:commons-lang3:3.8'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation 'org.jsoup:jsoup:1.13.1'  // 降级支持 4.4
    implementation 'com.blankj:utilcodex:1.31.0'
    implementation 'com.github.getActivity:XXPermissions:18.5'
    implementation 'org.apache.commons:commons-io:2.6'
    implementation 'com.github.seven332:jcifs:1.3.19'
    implementation 'io.github.inflationx:viewpump:2.1.1'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'com.yanzhenjie.andserver:api:2.1.12'
    kapt 'com.yanzhenjie.andserver:processor:2.1.12'
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    configurations {
        configureEach {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }
}
